#QueryScope=Work
#QueryReturnType=Graph
#QueryResults=A graph for the given work
#QueryParams=R_RES
#QueryUrl=/lib/resForMarc?R_RES=bdr:MW22084_0044-31

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the chosen resource

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct {
  ?R_RES ?p ?o .
  ?o ?op ?oo .
  ?t skos:prefLabel ?to .
  ?series skos:prefLabel ?seriesl .
  ?part ?partp ?parto .
  ?R_RES tmp:pubEvent ?we .
  ?we ?wep ?weo .
  ?aac ?aacp ?aaco .
  ?aac tmp:agentSameAs ?o .
  ?id ?idp ?ido .
  ?o skos:prefLabel ?ol .
  ?R_RES bf:identifiedBy ?id .
} where {
  {
    ?R_RES ?p ?o .
  }
  union {
    ?R_RES :instanceReproductionOf ?mw .
    ?mw ?p ?o .
  }
  union {
    ?R_RES :instanceOf ?wa .
    ?wa ?p ?o .
  }
  union {
    ?R_RES :instanceHasReproduction ?item .
    ?item a :ImageInstance ;
          ?p ?o .
  }
  union {
    ?R_RES :instanceReproductionOf?/bdo:partOf+ ?part .
    ?part ?partp ?parto .
    FILTER (?partp = skos:prefLabel || ?partp = bdo:partType || ?partp = bdo:partOf)
  }
  union {
    ?R_RES :instanceReproductionOf?/bdo:serialInstanceOf ?series .
    ?series skos:prefLabel ?seriesl .
  }
  union {
    ?R_RES bdo:inRootInstance?/bdo:instanceOf?/bdo:creator ?aac .
    ?aac ?aacp ?aaco .
    ?aac bdo:agent/owl:sameAs? ?o .
    ?o skos:prefLabel ?ol .
  }
  union {
    ?R_RES :instanceReproductionOf?/bdo:inRootInstance?/bf:identifiedBy ?id .
    ?id ?idp ?ido .
  }
  union {
    ?R_RES :instanceReproductionOf?/bdo:inRootInstance?/:instanceEvent ?we .
    ?we a :PublishedEvent ;
      ?wep ?weo .
  }
}
